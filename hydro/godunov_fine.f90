module godunov_fine_module
#ifdef _CUDA
  use gpu_runner, only: gpu_godunov, gpu_set_unew, gpu_set_uold
#endif
contains
!###########################################################
!###########################################################
!###########################################################
!###########################################################
recursive subroutine r_godunov_fine(pst,ilevel,input_size)
  use mdl_module
  use ramses_commons, only: pst_t
  use mdl_parameters
  implicit none
  type(pst_t)::pst
  integer,VALUE::input_size
  integer::ilevel

  integer::rID

  if(pst%nLower>0)then
     rID = mdl_send_request(pst%s%mdl,MDL_GODUNOV_FINE,pst%iUpper+1,input_size,0,ilevel)
     call r_godunov_fine(pst%pLower,ilevel,input_size)
     call mdl_get_reply(pst%s%mdl,rID,0)
  else
#ifdef _CUDA
     call gpu_godunov(pst%s, ilevel)
#else
     call godunov_fine(pst%s, ilevel)
#endif
  endif

end subroutine r_godunov_fine
!###########################################################
!###########################################################
!###########################################################
!###########################################################
subroutine godunov_fine(s,ilevel)
  use ramses_commons, only: ramses_t
  use cache_commons
  use cache
  use marshal, only: pack_fetch_refine, unpack_fetch_refine
  use boundaries, only: init_bound_refine
  implicit none
  type(ramses_t)::s
  integer::ilevel
  type(msg_large_realdp)::dummy_large_realdp
  !--------------------------------------------------------------------------
  ! This routine is a wrapper to the second order Godunov solver.
  ! Small grids (2x2x2) are gathered from level ilevel and sent to the
  ! hydro solver. On entry, hydro variables are gathered from array uold.
  ! On exit, unew has been updated. 
  !--------------------------------------------------------------------------
  integer::igrid

  if(s%r%verbose.and.s%g%myid==1)write(*,'("   Entering godunov_fine for level ",I2)')ilevel

  associate(r=>s%r,g=>s%g,m=>s%m,mdl=>s%mdl)

  call open_cache(mdl, m, pack_size=storage_size(dummy_large_realdp)/32, &
       pack=pack_fetch_refine, unpack=unpack_fetch_refine, &
       init=init_flush_godunov, flush=pack_flush_godunov, &
       combine=unpack_flush_godunov, bound=init_bound_refine)

  ! Loop over active grids by vector sweeps
  igrid=m%head(ilevel)
  do while(igrid.LE.m%tail(ilevel))
     SELECT CASE (m%grid(igrid)%superoct)
     CASE(1)
        call godfine1(s,igrid,ilevel,m%hydro_w%kernel_1)
     CASE(2**ndim)
        call godfine1(s,igrid,ilevel,m%hydro_w%kernel_2)
     CASE(4**ndim)
        call godfine1(s,igrid,ilevel,m%hydro_w%kernel_4)
     CASE(8**ndim)
        call godfine1(s,igrid,ilevel,m%hydro_w%kernel_8)
     CASE(16**ndim)
        call godfine1(s,igrid,ilevel,m%hydro_w%kernel_16)
     CASE(32**ndim)
        call godfine1(s,igrid,ilevel,m%hydro_w%kernel_32)
     END SELECT
     igrid=igrid+m%grid(igrid)%superoct
  end do

  call close_cache(mdl)

  end associate
end subroutine godunov_fine
!###########################################################
!###########################################################
!###########################################################
!###########################################################
recursive subroutine r_set_unew(pst,ilevel,input_size)
  use mdl_module
  use ramses_commons, only: pst_t
  use mdl_parameters
  implicit none
  type(pst_t)::pst
  integer,VALUE::input_size
  integer::ilevel

  integer::rID

  if(pst%nLower>0)then
     rID = mdl_send_request(pst%s%mdl,MDL_SET_UNEW,pst%iUpper+1,input_size,0,ilevel)
     call r_set_unew(pst%pLower,ilevel,input_size)
     call mdl_get_reply(pst%s%mdl,rID,0)
  else
#ifdef _CUDA
     call gpu_set_unew(pst%s, ilevel)
#else
     call set_unew(pst%s%r,pst%s%g,pst%s%m,ilevel)
#endif
  endif

end subroutine r_set_unew
!###########################################################
!###########################################################
!###########################################################
!###########################################################
subroutine set_unew(r,g,m,ilevel)
  use amr_parameters, only: ndim, twotondim
  use amr_commons, only: run_t, global_t, mesh_t
  implicit none
  type(run_t)::r
  type(global_t)::g
  type(mesh_t)::m
  integer::ilevel
  !--------------------------------------------------------------------------
  ! This routine sets array unew to its initial value uold before calling
  ! the hydro scheme. unew is set to zero in virtual boundaries.
  !--------------------------------------------------------------------------
  integer::i

#ifdef HYDRO
  ! Set unew to uold for myid cells
  !$OMP PARALLEL DO
  do i = m%head(ilevel),m%tail(ilevel)
     m%unew(:,:,i) = m%uold(:,:,i)
#ifdef TRCFLX
     m%mflux(:,:,i) = 0.0d0
#endif
  end do
  !$OMP END PARALLEL DO
#endif
#ifdef MHD
  ! Set bnew to bold for myid cells
  !$OMP PARALLEL DO
  do i = m%head(ilevel),m%tail(ilevel)
     m%bnew(:,:,i) = m%bold(:,:,i)
  end do
  !$OMP END PARALLEL DO
#endif

end subroutine set_unew
!###########################################################
!###########################################################
!###########################################################
!###########################################################
recursive subroutine r_set_uold(pst,ilevel,input_size)
  use mdl_module
  use ramses_commons, only: pst_t
  use mdl_parameters
  implicit none
  type(pst_t)::pst
  integer,VALUE::input_size
  integer::ilevel
  integer::rID

  if(pst%nLower>0)then
     rID = mdl_send_request(pst%s%mdl,MDL_SET_UOLD,pst%iUpper+1,input_size,0,ilevel)
     call r_set_uold(pst%pLower,ilevel,input_size)
     call mdl_get_reply(pst%s%mdl,rID,0)
  else
#ifdef _CUDA
     call gpu_set_uold(pst%s, ilevel)
#else
     call set_uold(pst%s%r,pst%s%g,pst%s%m,ilevel)
#endif
  endif

end subroutine r_set_uold
!###########################################################
!###########################################################
!###########################################################
!###########################################################
subroutine set_uold(r,g,m,ilevel)
  use amr_parameters, only: ndim, twotondim
  use amr_commons, only: run_t, global_t, mesh_t
  implicit none
  type(run_t)::r
  type(global_t)::g
  type(mesh_t)::m
  integer::ilevel
  !---------------------------------------------------------
  ! This routine sets array uold to its new value unew 
  ! after the hydro step.
  !---------------------------------------------------------
  integer::i

#ifdef HYDRO
  ! Set uold to unew
  !$OMP PARALLEL DO
  do i = m%head(ilevel),m%tail(ilevel)
     m%uold(:,:,i) = m%unew(:,:,i)
  end do
  !$OMP END PARALLEL DO
#endif
#ifdef MHD
  ! Set bold to bnew
  !$OMP PARALLEL DO
  do i = m%head(ilevel),m%tail(ilevel)
     m%bold(:,:,i) = m%bnew(:,:,i)
  end do
  !$OMP END PARALLEL DO
#endif

end subroutine set_uold
!###########################################################
!###########################################################
!###########################################################
!###########################################################
subroutine godfine1(s,ind_grid,ilevel,h)
  use mdl_module
  use amr_parameters, only: ndim, twondim, twotondim
  use hydro_parameters, only: nvar
  use ramses_commons, only: ramses_t
  use nbors_utils
  use hydro_commons
  implicit none
  type(ramses_t)::s
  integer::ind_grid,ilevel
  type(hydro_kernel_t)::h
  !-------------------------------------------------------------------
  ! This routine gathers first hydro variables from neighboring grids
  ! to set initial conditions in a 6x6x6 grid. It interpolate from
  ! coarser level missing grid variables. It then calls the
  ! Godunov solver that computes fluxes. These fluxes are zeroed at 
  ! coarse-fine boundaries, since contribution from finer levels has
  ! already been taken into account. Conservative variables are updated 
  ! and stored in array unew(:), both at the current level and at the 
  ! coarser level if necessary.
  !-------------------------------------------------------------------
  integer::ivar,idim,ind,ind_son,ind_oct
  integer::icell,inbor,ipass
  integer::i0,j0,k0,i1,j1,k1,i2,j2,k2,i3,j3,k3
  integer::ii0,jj0,kk0,ii1,jj1,kk1
  integer::i1min,i1max,j1min,j1max,k1min,k1max
  integer::ii1min,ii1max,jj1min,jj1max,kk1min,kk1max
  integer::i2min,i2max,j2min,j2max,k2min,k2max
  integer::i3min,i3max,j3min,j3max,k3min,k3max
#ifdef MHD
  integer,dimension(1:8,1:3)::jj
  real(kind=8),dimension(0:twondim  ,1:6)::b1
  real(kind=8),dimension(1:twotondim,1:6)::b2
  real(kind=8),dimension(1:twondim,1:twotondim,1:6)::b3
  logical,dimension(1:twondim)::refined
  integer,dimension(1:3,1:6),save::shift=reshape(&
       & (/-1,0,0,1,0,0,0,-1,0,0,1,0,0,0,-1,0,0,1/),(/3,6/))
  integer::igrid1,igrid2,igrid3,igridn
  integer::icell1,icell2,icell3
  real(kind=8)::dflux,dflux_x,dflux_y,dflux_z,weight
  integer,dimension(1:twondim)::igrid_son_nbor
#endif
  integer,dimension(1:ndim)::ckey_corner,ckey
  integer(kind=8),dimension(0:ndim)::hash_nbor,hash_son_nbor
  integer,dimension(0:twondim)::ind_nbor
  integer,dimension(0:twondim)::igrid_nbor
  real(kind=8)::dx,oneontwotondim
  real(kind=8),dimension(0:twondim  ,1:nvar)::u1
  real(kind=8),dimension(1:twotondim,1:nvar)::u2
  logical::okx,oky,okz,oknbor
  logical::ok1,ok2,ok3
  integer::igrid,ichild
  real(kind=8)::fluxL,fluxR

#ifdef MHD
  jj(1:8,1)=(/4,1,4,1,4,1,4,1/)
  jj(1:8,2)=(/5,5,2,2,5,5,2,2/)
  jj(1:8,3)=(/6,6,6,6,3,3,3,3/)
#endif
  i2min=0; i2max=0; j2min=0; j2max=0; k2min=0; k2max=0
  i3min=1; i3max=1; j3min=1; j3max=1; k3min=1; k3max=1
  okx=.true.; oky=.true.; okz=.true.

#ifdef HYDRO

  associate(r=>s%r,g=>s%g,m=>s%m,mdl=>s%mdl)

  oneontwotondim=1.d0/dble(twotondim)

  ! Mesh spacing in that level
  dx=r%boxlen/2**ilevel

  ! Integer constants
  i1min=h%io1; i1max=h%io2; j1min=h%jo1; j1max=h%jo2; k1min=h%ko1; k1max=h%ko2
#if NDIM>0
  i2max=1; i3min=h%iu1+2; i3max=h%iu2-2
#endif
#if NDIM>1
  j2max=1; j3min=h%ju1+2; j3max=h%ju2-2
#endif
#if NDIM>2
  k2max=1; k3min=h%ku1+2; k3max=h%ku2-2
#endif

  !---------------------
  ! Gather hydro stencil
  !---------------------
  hash_nbor(0)=m%grid(ind_grid)%lev
  ckey_corner(1:ndim)=(m%grid(ind_grid)%ckey(1:ndim)/(i1max-1))*(i1max-1)
  ind_oct=ind_grid

  ! Loop over 3x3x3 neighboring father cells using 27 passes
  do ipass = 1, threetondim

  if(ipass == 1)then
     ii1min = i1min+1; ii1max = i1max-1
     jj1min = j1min; jj1max = j1max
#if NDIM>1
     jj1min = jj1min+1; jj1max = jj1max-1
#endif
     kk1min = k1min; kk1max = k1max;
#if NDIM>2
     kk1min = kk1min+1; kk1max = kk1max-1
#endif
  endif
  if(MOD(ipass,3) == 2)then ! ipass = 2, 5, 8, 11, 14, 17, 20, 23, 26
     ii1min = i1min; ii1max = i1min
  endif
  if(MOD(ipass,3) == 0)then ! ipass = 3, 6, 9, 12, 15, 18, 21, 24, 27
     ii1min = i1max; ii1max = i1max
  endif
  if(MOD(ipass,9) == 4)then ! ipass = 4, 13, 22
     ii1min = i1min+1; ii1max = i1max-1
     jj1min = j1min; jj1max = j1min
  endif
  if(MOD(ipass,9) == 7)then ! ipass = 7, 16, 25
     ii1min = i1min+1; ii1max = i1max-1
     jj1min = j1max; jj1max = j1max
  endif
  if(ipass == 10)then
     ii1min = i1min+1; ii1max = i1max-1
     jj1min = j1min+1; jj1max = j1max-1
     kk1min = k1min; kk1max = k1min
  endif
  if(ipass == 19)then
     ii1min = i1min+1; ii1max = i1max-1
     jj1min = j1min+1; jj1max = j1max-1
     kk1min = k1max; kk1max = k1max
  endif

  do k1=kk1min,kk1max
#if NDIM>2
     okz=(k1>k1min.and.k1<k1max)
#endif
     do j1=jj1min,jj1max
#if NDIM>1
        oky=(j1>j1min.and.j1<j1max)
#endif
        do i1=ii1min,ii1max
           okx=(i1>i1min.and.i1<i1max)

           !--------------------
           ! For inner octs only
           !--------------------
           if(okx.and.oky.and.okz)then

              ! Compute relative Cartesian key
              ckey(1:ndim)=m%grid(ind_oct)%ckey(1:ndim)-ckey_corner(1:ndim)

              ! Store grid index
              ii1=1; jj1=1; kk1=1
              ii1=ckey(1)+1
#if NDIM>1
              jj1=ckey(2)+1
#endif
#if NDIM>2
              kk1=ckey(3)+1
#endif
              h%childloc(ii1,jj1,kk1)=ind_oct
              h%gridloc(ii1,jj1,kk1)=0
              h%cellloc(ii1,jj1,kk1)=0

              ! Loop over 2x2x2 cells
              do k2=k2min,k2max
                 do j2=j2min,j2max
                    do i2=i2min,i2max
                       ind_son=1+i2+2*j2+4*k2
                       i3=1; j3=1; k3=1
                       i3=1+2*(ii1-1)+i2
#if NDIM>1
                       j3=1+2*(jj1-1)+j2
#endif
#if NDIM>2
                       k3=1+2*(kk1-1)+k2
#endif             
                       ! Gather hydro variables
                       do ivar=1,nvar
                          h%uloc(i3,j3,k3,ivar)=m%uold(ind_son,ivar,ind_oct)
                       end do
#ifdef MHD
                       ! Gather MHD variables
                       do ivar=1,6
                          h%bloc(i3,j3,k3,ivar)=m%bold(ind_son,ivar,ind_oct)
                       end do
#endif
#ifdef GRAV
                       ! Gather self-gravitational acceleration
                       do idim=1,ndim
                          h%gloc(i3,j3,k3,idim)=m%f(ind_son,idim,ind_oct)
                       end do
#else
                       ! Gather constant gravitational acceleration
                       do idim=1,ndim
                          h%gloc(i3,j3,k3,idim)=r%constant_gravity(idim)
                       end do
#endif
                       ! Gather refinement flag
                       h%okloc(i3,j3,k3)=m%grid(ind_oct)%refined(ind_son)
                    end do
                 end do
              end do

              ! Go to next inner oct
              ind_oct=ind_oct+1

           !-----------------------
           ! For boundary octs only
           !-----------------------
           else

              ! Compute neighboring grid Cartesian index
              hash_nbor(1)=ckey_corner(1)+i1-1
#if NDIM>1
              hash_nbor(2)=ckey_corner(2)+j1-1
#endif
#if NDIM>2
              hash_nbor(3)=ckey_corner(3)+k1-1
#endif
              ! Periodic boundary conditions
              do idim=1,ndim
                 if(r%periodic(idim))then
                    if(hash_nbor(idim)< m%box_ckey_min(idim,ilevel))hash_nbor(idim)=m%box_ckey_max(idim,ilevel)-1
                    if(hash_nbor(idim)>=m%box_ckey_max(idim,ilevel))hash_nbor(idim)=m%box_ckey_min(idim,ilevel)
                 endif
              enddo

              ! Set grid index to null
              icell=0
              igrid=0
              do inbor=0,twondim
                 igrid_nbor(inbor)=0
              end do
#ifdef MHD
              do inbor=1,twondim
                 igrid_son_nbor(inbor)=0
              end do
#endif
              ! Get neighboring grid index with read-only cache
              call get_grid(s,hash_nbor,ichild,flush_cache=.false.,fetch_cache=.true.,lock=.true.)

              !----------------------------------------------------
              ! If grid does not exist, interpolate hydro variables
              !----------------------------------------------------
              if(ichild==0)then

                 ! Get parent father cell with read-write cache
                 call get_parent_cell(s,hash_nbor,igrid,icell,flush_cache=.true.,fetch_cache=.true.,lock=.true.)
                 if(igrid==0)then
                    write(*,*)'GODUNOV: parent_cell should exist'
                    write(*,*)'PE ',g%myid,hash_nbor
                    call mdl_abort(mdl)
                 endif

                 ! Get 2ndim neighboring father cells with read-write cache
                 ! Note that possible cache grids are locked inside the routine
                 call get_twondim_nbor_parent_cell(s,hash_nbor,igrid_nbor,ind_nbor,flush_cache=.true.,fetch_cache=.true.)
                 oknbor=.true.
                 do inbor=0,twondim
                    oknbor=oknbor.and.(igrid_nbor(inbor)>0)
                 end do
                 if(.not. oknbor)then
                    write(*,*)"GODUNOV: parent neighbors should exist"
                    write(*,*)'PE ',g%myid,hash_nbor
                    write(*,*)igrid_nbor(0)
                    do idim=1,ndim
                       write(*,*)igrid_nbor(2*idim-1)
                       write(*,*)igrid_nbor(2*idim)
                    end do
                    call mdl_abort(mdl)
                 endif

                 ! Gather hydro variables
                 do inbor=0,twondim
                    do ivar=1,nvar
                       u1(inbor,ivar)=m%uold(ind_nbor(inbor),ivar,igrid_nbor(inbor))
                    end do
                 end do
#ifdef MHD
                 ! Gather MHD variables
                 do inbor=0,twondim
                    do ivar=1,6
                       b1(inbor,ivar)=m%bold(ind_nbor(inbor),ivar,igrid_nbor(inbor))
                    end do
                 end do
                 ! Get neighboring children grids
                 hash_son_nbor(0)=ilevel
                 do inbor=1,twondim
                    hash_son_nbor(1:ndim)=hash_nbor(1:ndim)+shift(1:ndim,inbor)
                    ! Periodic boundary conditions
                    do idim=1,ndim
                       if(r%periodic(idim))then
                          if(hash_son_nbor(idim)< m%box_ckey_min(idim,ilevel))hash_son_nbor(idim)=m%box_ckey_max(idim,ilevel)-1
                          if(hash_son_nbor(idim)>=m%box_ckey_max(idim,ilevel))hash_son_nbor(idim)=m%box_ckey_min(idim,ilevel)
                       endif
                    enddo
                    call get_grid(s,hash_son_nbor,igridn,flush_cache=.false.,fetch_cache=.true.,lock=.true.)
                    igrid_son_nbor(inbor)=igridn
                    refined(inbor)=(igridn>0)
                    if(refined(inbor))then
                       do ind=1,twotondim
                          do ivar=1,6
                             b3(inbor,ind,ivar)=m%bold(ind,ivar,igridn)
                          end do
                       end do
                    endif
                 end do

                 ! Interpolate using MHD variables
                 call interpol_mhd(u1,u2,b1,b2,b3,refined,r%interpol_var,r%interpol_type,r%smallr)
#else
                 ! Interpolate using hydro variables
                 call interpol_hydro(u1,u2,r%interpol_var,r%interpol_type,r%smallr)
#endif
              endif

              ! Store grid index
              h%childloc(i1,j1,k1)=ichild
              h%gridloc (i1,j1,k1)=igrid
              h%cellloc (i1,j1,k1)=icell
              do inbor=1,twondim
                 h%nborloc(i1,j1,k1,inbor)=igrid_nbor(inbor)
#ifdef MHD
                 h%nborsonloc(i1,j1,k1,inbor)=igrid_son_nbor(inbor)
#endif
              end do

              ! Loop over 2x2x2 cells
              do k2=k2min,k2max
                 do j2=j2min,j2max
                    do i2=i2min,i2max                       
                       ind_son=1+i2+2*j2+4*k2
                       i3=1; j3=1; k3=1
                       i3=1+2*(i1-1)+i2
#if NDIM>1
                       j3=1+2*(j1-1)+j2
#endif
#if NDIM>2
                       k3=1+2*(k1-1)+k2
#endif             
                       !----------------------------------------------------
                       ! If neighboring grid exists, use refined grid values
                       !----------------------------------------------------
                       if(ichild>0)then

                          ! Gather hydro variables
                          do ivar=1,nvar
                             h%uloc(i3,j3,k3,ivar)=m%uold(ind_son,ivar,ichild)
                          end do
#ifdef MHD
                          ! Gather MHD variables
                          do ivar=1,6
                             h%bloc(i3,j3,k3,ivar)=m%bold(ind_son,ivar,ichild)
                          end do
#endif
#ifdef GRAV
                          ! Gather self-gravitational acceleration
                          do idim=1,ndim
                             h%gloc(i3,j3,k3,idim)=m%f(ind_son,idim,ichild)
                          end do
#else
                          ! Gather constant gravitational acceleration
                          do idim=1,ndim
                             h%gloc(i3,j3,k3,idim)=r%constant_gravity(idim)
                          end do
#endif
                          ! Gather refinement flag
                          h%okloc(i3,j3,k3)=m%grid(ichild)%refined(ind_son)

                       !-----------------------------------------------------------
                       ! If neighboring grid doesn't exist, use interpolated values
                       !-----------------------------------------------------------
                       else

                          ! Gather interpolated hydro variables
                          do ivar=1,nvar
                             h%uloc(i3,j3,k3,ivar)=u2(ind_son,ivar)
                          end do
#ifdef MHD
                          ! Gather interpolated MHD variables
                          do ivar=1,6
                             h%bloc(i3,j3,k3,ivar)=b2(ind_son,ivar)
                          end do
#endif
#ifdef GRAV
                          ! Gather self-gravitational acceleration
                          do idim=1,ndim
                             h%gloc(i3,j3,k3,idim)=m%f(icell,idim,igrid)
                          end do
#else
                          ! Gather constant gravitational acceleration
                          do idim=1,ndim
                             h%gloc(i3,j3,k3,idim)=r%constant_gravity(idim)
                          end do
#endif
                          ! Gather refinement flag
                          h%okloc(i3,j3,k3)=.false.
                       end if

                    end do
                 end do
              end do
              ! End loop over 2x2x2 cells
           endif
        end do
     end do
  end do
  ! End over octs
  end do

  !-----------------------------------------------
  ! Compute flux using second-order Godunov method
  !-----------------------------------------------
  call unsplit(h%uloc,h%gloc,h%qloc,h%cloc,&
       & h%flux,h%tmp,h%dq,h%qm,h%qp,h%fx,h%tx,h%divu,&
#ifdef MHD
       & h%bloc,h%emfx,h%emfy,h%emfz,h%bf,h%dbf,h%Ex,h%Ey,h%Ez,h%qRT,h%qRB,h%qLT,h%qLB,&
#endif
       & dx,g%dtnew(ilevel),&
       & h%iu1,h%iu2,h%ju1,h%ju2,h%ku1,h%ku2,&
       & h%if1,h%if2,h%jf1,h%jf2,h%kf1,h%kf2,&
       & s%h_params)

  !-------------------------------------------------
  ! Reset flux along direction at refined interfaces
  !-------------------------------------------------
  do idim=1,ndim
     i0=0; j0=0; k0=0
     if(idim==1)i0=1
     if(idim==2)j0=1
     if(idim==3)k0=1
     do k3=k3min,k3max+k0
        do j3=j3min,j3max+j0
           do i3=i3min,i3max+i0
              if(h%okloc(i3-i0,j3-j0,k3-k0) .or. h%okloc(i3,j3,k3))then
                 do ivar=1,nprim
                    h%flux(i3,j3,k3,ivar,idim)=0.0d0
                 end do
              end if
           end do
        end do
     end do
  end do
#ifdef MHD
  ! In case of pure induction, set Euler fluxes to zero
  if(r%induction)h%flux=0.0d0
#if NDIM>1
  !---------------------------------------------------
  ! Reset electromotive force along z at refined edges
  !---------------------------------------------------
  do k3=k3min,k3max
     do j3=j3min,j3max+1
        do i3=i3min,i3max+1
           if(    h%okloc(i3-1,j3-1,k3) .or. h%okloc(i3  ,j3-1,k3) .or.  &
                & h%okloc(i3-1,j3  ,k3) .or. h%okloc(i3  ,j3  ,k3))then
              h%emfz(i3,j3,k3)=0.0d0
           end if
        end do
     end do
  end do
#if NDIM==3
  !---------------------------------------------------
  ! Reset electromotive force along y at refined edges
  !---------------------------------------------------
  do j3=j3min,j3max
     do i3=i3min,i3max+1
        do k3=k3min,k3max+1
           if(    h%okloc(i3-1,j3,k3-1) .or. h%okloc(i3  ,j3,k3-1) .or.  &
                & h%okloc(i3-1,j3,k3  ) .or. h%okloc(i3  ,j3,k3  ))then
              h%emfy(i3,j3,k3)=0.0d0
           end if
        end do
     end do
  end do
  !---------------------------------------------------
  ! Reset electromotive force along x at refined edges
  !---------------------------------------------------
  do i3=i3min,i3max
     do j3=j3min,j3max+1
        do k3=k3min,k3max+1
           if(    h%okloc(i3,j3-1,k3-1) .or. h%okloc(i3,j3  ,k3-1) .or.  &
                & h%okloc(i3,j3-1,k3  ) .or. h%okloc(i3,j3  ,k3  ))then
              h%emfx(i3,j3,k3)=0.0d0
           end if
        end do
     end do
  end do
#endif
#endif
#endif
  !--------------------------------------
  ! Conservative update at level ilevel
  !--------------------------------------
  ! Loop over dimensions
  do idim=1,ndim
     i0=0; j0=0; k0=0
     ii0=0; jj0=0; kk0=0
     if(idim==1)i0=1
     if(idim==2)j0=1
     if(idim==3)k0=1
     ii0=1
#if NDIM>1
     jj0=1
#endif
#if NDIM>2
     kk0=1
#endif
     ! Loop over inner octs
     do k1=k1min+kk0,k1max-kk0
        do j1=j1min+jj0,j1max-jj0
           do i1=i1min+ii0,i1max-ii0
              ! Get oct index
              ichild=h%childloc(i1,j1,k1)
              ! Loop over cells
              do k2=k2min,k2max
                 do j2=j2min,j2max
                    do i2=i2min,i2max
                       ind_son=1+i2+2*j2+4*k2
                       i3=1; j3=1; k3=1
                       i3=1+2*(i1-1)+i2
#if NDIM>1
                       j3=1+2*(j1-1)+j2
#endif
#if NDIM>2
                       k3=1+2*(k1-1)+k2
#endif
                       ! Store old density for MC tracers
#ifdef TRCFLX
                       m%mflux(ind_son,1,ichild) = max(m%uold(ind_son,1,ichild), r%smallr)

                       ! Store time-integrated mass flux on the two faces along the current direction
                       fluxL = h%flux(i3   ,j3   ,k3   ,1,idim)
                       fluxR = h%flux(i3+i0,j3+j0,k3+k0,1,idim)
                       m%mflux(ind_son,1+idim,ichild)=fluxL
                       m%mflux(ind_son,1+ndim+idim,ichild)=fluxR
#endif
                       ! Update conservative variables new state vector
                       do ivar=1,5
                          m%unew(ind_son,ivar,ichild)=m%unew(ind_son,ivar,ichild)+ &
                               & (h%flux(i3   ,j3   ,k3   ,ivar,idim) &
                               & -h%flux(i3+i0,j3+j0,k3+k0,ivar,idim))
                       end do
#if NVAR>5
                       do ivar=6,nvar
                          m%unew(ind_son,ivar,ichild)=m%unew(ind_son,ivar,ichild)+ &
                               & (h%flux(i3   ,j3   ,k3   ,ivar+ie-5,idim) &
                               & -h%flux(i3+i0,j3+j0,k3+k0,ivar+ie-5,idim))
                       end do
#endif
#ifdef MHD
#if NDIM<3
                       m%bnew(ind_son,3,ichild)=m%bnew(ind_son,3,ichild)+ &
                            & (h%flux(i3   ,j3   ,k3   ,8,idim) &
                            & -h%flux(i3+i0,j3+j0,k3+k0,8,idim))
                       m%bnew(ind_son,6,ichild)=m%bnew(ind_son,6,ichild)+ &
                            & (h%flux(i3   ,j3   ,k3   ,8,idim) &
                            & -h%flux(i3+i0,j3+j0,k3+k0,8,idim))
#if NDIM==1
                       m%bnew(ind_son,2,ichild)=m%bnew(ind_son,2,ichild)+ &
                            & (h%flux(i3   ,j3   ,k3   ,7,idim) &
                            & -h%flux(i3+i0,j3+j0,k3+k0,7,idim))
                       m%bnew(ind_son,5,ichild)=m%bnew(ind_son,5,ichild)+ &
                            & (h%flux(i3   ,j3   ,k3   ,7,idim) &
                            & -h%flux(i3+i0,j3+j0,k3+k0,7,idim))
#endif
#endif
#endif
                    end do
                 end do
              end do
           end do
        end do
     end do
  end do
#ifdef MHD
#if NDIM>1

  !----------------------------------------
  ! Induction system update at level ilevel
  !----------------------------------------
  ii0=1; jj0=1; kk0=0
#if NDIM==3
  kk0=1
#endif
  ! Loop over inner octs
  do k1=k1min+kk0,k1max-kk0
     do j1=j1min+jj0,j1max-jj0
        do i1=i1min+ii0,i1max-ii0
           ! Get oct index
           ichild=h%childloc(i1,j1,k1)
           ! Loop over cells
           do k2=k2min,k2max
              do j2=j2min,j2max
                 do i2=i2min,i2max
                    ind_son=1+i2+2*j2+4*k2
                    i3=1+2*(i1-1)+i2
                    j3=1+2*(j1-1)+j2
                    k3=1
#if NDIM==3
                    k3=1+2*(k1-1)+k2
#endif
                    ! Update Bx using constrained transport
#if NDIM==3
                    dflux_x=( h%emfy(i3,j3,k3)-h%emfy(i3,j3,k3+1) ) &
                         & -( h%emfz(i3,j3,k3)-h%emfz(i3,j3+1,k3) )
#else
                    dflux_x=-(h%emfz(i3,j3,k3)-h%emfz(i3,j3+1,k3))
#endif
                    m%bnew(ind_son,1,ichild)=m%bnew(ind_son,1,ichild)+dflux_x
#if NDIM==3
                    dflux_x=( h%emfy(i3+1,j3,k3)-h%emfy(i3+1,j3,k3+1) ) &
                         & -( h%emfz(i3+1,j3,k3)-h%emfz(i3+1,j3+1,k3) )
#else
                    dflux_x=-(h%emfz(i3+1,j3,k3)-h%emfz(i3+1,j3+1,k3))
#endif
                    m%bnew(ind_son,4,ichild)=m%bnew(ind_son,4,ichild)+dflux_x

                    ! Update By using constrained transport
#if NDIM==3
                    dflux_y=( h%emfz(i3,j3,k3)-h%emfz(i3+1,j3,k3) ) &
                         & -( h%emfx(i3,j3,k3)-h%emfx(i3,j3,k3+1) )
#else
                    dflux_y=(h%emfz(i3,j3,k3)-h%emfz(i3+1,j3,k3))
#endif
                    m%bnew(ind_son,2,ichild)=m%bnew(ind_son,2,ichild)+dflux_y
#if NDIM==3
                    dflux_y=( h%emfz(i3,j3+1,k3)-h%emfz(i3+1,j3+1,k3) ) &
                         & -( h%emfx(i3,j3+1,k3)-h%emfx(i3,j3+1,k3+1) )
#else
                    dflux_y=(h%emfz(i3,j3+1,k3)-h%emfz(i3+1,j3+1,k3))
#endif
                    m%bnew(ind_son,5,ichild)=m%bnew(ind_son,5,ichild)+dflux_y
#if NDIM==3
                    ! Update Bz using constrained transport
                    dflux_z=( h%emfx(i3,j3,k3)-h%emfx(i3,j3+1,k3) ) &
                         & -( h%emfy(i3,j3,k3)-h%emfy(i3+1,j3,k3) )
                    m%bnew(ind_son,3,ichild)=m%bnew(ind_son,3,ichild)+dflux_z
                    dflux_z=( h%emfx(i3,j3,k3+1)-h%emfx(i3,j3+1,k3+1) ) &
                         & -( h%emfy(i3,j3,k3+1)-h%emfy(i3+1,j3,k3+1) )
                    m%bnew(ind_son,6,ichild)=m%bnew(ind_son,6,ichild)+dflux_z
#endif
                 end do
              end do
           end do
        end do
     end do
  end do
#endif
#endif

  ! If coarsest level, skip.
  if(ilevel>r%levelmin)then

  !--------------------------------------
  ! Conservative update at level ilevel-1
  !--------------------------------------
  ! Loop over dimensions
  do idim=1,ndim
     i0=0; j0=0; k0=0
     ii0=0; jj0=0; kk0=0
     if(idim==1)i0=1
     if(idim==2)j0=1
     if(idim==3)k0=1
     ii0=1
#if NDIM>1
     jj0=1
#endif
#if NDIM>2
     kk0=1
#endif
     ii1min=i1min+ii0; ii1max=i1max-ii0
     jj1min=j1min+jj0; jj1max=j1max-jj0
     kk1min=k1min+kk0; kk1max=k1max-kk0
     !----------------------
     ! Left flux at boundary
     !----------------------     
     if(idim==1)then
        ii1min=i1min; ii1max=i1min
     endif
     if(idim==2)then
        jj1min=j1min; jj1max=j1min
     endif
     if(idim==3)then
        kk1min=k1min; kk1max=k1min
     endif
     ! Loop over outer octs on left face
     do k1=kk1min,kk1max
        do j1=jj1min,jj1max
           do i1=ii1min,ii1max
              ! Get oct index
              ichild=h%childloc(i1,j1,k1)
              ! Check that parent cell is not refined
              if(ichild==0)then
                 ! Get parent cell index
                 igrid=h%gridloc(i1,j1,k1)
                 icell=h%cellloc(i1,j1,k1)
                 ! Loop over inner cell left faces
                 do k2=k2min,k2max-k0
                    do j2=j2min,j2max-j0
                       do i2=i2min,i2max-i0
                          i3=1; j3=1; k3=1
                          i3=1+2*(i1+i0-1)+i2
#if NDIM>1
                          j3=1+2*(j1+j0-1)+j2
#endif
#if NDIM>2
                          k3=1+2*(k1+k0-1)+k2
#endif
                          ! Conservative update of new state variables
                          do ivar=1,5
                             m%unew(icell,ivar,igrid)=m%unew(icell,ivar,igrid) &
                                  & -h%flux(i3,j3,k3,ivar,idim)*oneontwotondim
                          end do
#if NVAR>5
                          do ivar=6,nvar
                             m%unew(icell,ivar,igrid)=m%unew(icell,ivar,igrid) &
                                  & -h%flux(i3,j3,k3,ivar+ie-5,idim)*oneontwotondim
                          end do
#endif
#ifdef MHD
#if NDIM<3
                          m%bnew(icell,3,igrid)=m%bnew(icell,3,igrid) &
                               & -h%flux(i3,j3,k3,8,idim)*oneontwotondim
                          m%bnew(icell,6,igrid)=m%bnew(icell,6,igrid) &
                               & -h%flux(i3,j3,k3,8,idim)*oneontwotondim
#if NDIM==1
                          m%bnew(icell,2,igrid)=m%bnew(icell,2,igrid) &
                               & -h%flux(i3,j3,k3,7,idim)*oneontwotondim
                          m%bnew(icell,5,igrid)=m%bnew(icell,5,igrid) &
                               & -h%flux(i3,j3,k3,7,idim)*oneontwotondim
#endif
#endif
#endif
                       end do
                    end do
                 end do
              endif
           end do
        end do
     end do
     !-----------------------
     ! Right flux at boundary
     !-----------------------     
     if(idim==1)then
        ii1min=i1max; ii1max=i1max
     endif
     if(idim==2)then
        jj1min=j1max; jj1max=j1max
     endif
     if(idim==3)then
        kk1min=k1max; kk1max=k1max
     endif
     ! Loop over outer octs on right face
     do k1=kk1min,kk1max
        do j1=jj1min,jj1max
           do i1=ii1min,ii1max
              ! Get oct index
              ichild=h%childloc(i1,j1,k1)
              ! Check that parent cell is not refined
              if(ichild==0)then
                 ! Get parent cell index
                 igrid=h%gridloc(i1,j1,k1)
                 icell=h%cellloc(i1,j1,k1)
                 ! Loop over inner cell right faces
                 do k2=k2min+k0,k2max
                    do j2=j2min+j0,j2max
                       do i2=i2min+i0,i2max
                          i3=1; j3=1; k3=1
                          i3=1+2*(i1-i0-1)+i2
#if NDIM>1
                          j3=1+2*(j1-j0-1)+j2
#endif
#if NDIM>2
                          k3=1+2*(k1-k0-1)+k2
#endif
                          ! Conservative update of new state variables
                          do ivar=1,5
                             m%unew(icell,ivar,igrid)=m%unew(icell,ivar,igrid) &
                                  & +h%flux(i3+i0,j3+j0,k3+k0,ivar,idim)*oneontwotondim
                          end do
#if NVAR>5
                          do ivar=6,nvar
                             m%unew(icell,ivar,igrid)=m%unew(icell,ivar,igrid) &
                                  & +h%flux(i3+i0,j3+j0,k3+k0,ivar+ie-5,idim)*oneontwotondim
                          end do
#endif
#ifdef MHD
#if NDIM<3
                          m%bnew(icell,3,igrid)=m%bnew(icell,3,igrid) &
                               & +h%flux(i3+i0,j3+j0,k3+k0,8,idim)*oneontwotondim
                          m%bnew(icell,6,igrid)=m%bnew(icell,6,igrid) &
                               & +h%flux(i3+i0,j3+j0,k3+k0,8,idim)*oneontwotondim
#if NDIM==1
                          m%bnew(icell,2,igrid)=m%bnew(icell,2,igrid) &
                               & +h%flux(i3+i0,j3+j0,k3+k0,7,idim)*oneontwotondim
                          m%bnew(icell,5,igrid)=m%bnew(icell,5,igrid) &
                               & +h%flux(i3+i0,j3+j0,k3+k0,7,idim)*oneontwotondim
#endif
#endif
#endif
                       end do
                    end do
                 end do
                 ! End loop over faces
              endif
           end do
        end do
     end do
     ! End loop over boundary octs
  end do
  ! End loop over dimensions

#ifdef MHD
#if NDIM>1
  !------------------------------------------
  ! Induction system update at level ilevel-1
  !------------------------------------------
  ii0=1; jj0=1
  kk0=0
#if NDIM==3
  kk0=1
#endif
  ii1min=i1min+ii0; ii1max=i1max-ii0

  jj1min=j1min+jj0; jj1max=j1max-jj0
  kk1min=k1min+kk0; kk1max=k1max-kk0
  ! Loop over inner octs
  do k1=kk1min,kk1max
     do j1=jj1min,jj1max
        do i1=ii1min,ii1max

           i3=1+2*(i1-1)
           j3=1+2*(j1-1)
           k3=1
#if NDIM==3
           k3=1+2*(k1-1)
#endif
           !--------------------------------------
           ! Deal with 4 EMFz edges
           !--------------------------------------

           ! Update coarse Bx and By using fine EMFz on X=0 and Y=0 grid edge
           ok1=(h%childloc(i1  ,j1-1,k1)>0); igrid1=h%gridloc(i1  ,j1-1,k1); icell1=h%cellloc(i1  ,j1-1,k1)
           ok2=(h%childloc(i1-1,j1-1,k1)>0); igrid2=h%gridloc(i1-1,j1-1,k1); icell2=h%cellloc(i1-1,j1-1,k1)
           ok3=(h%childloc(i1-1,j1  ,k1)>0); igrid3=h%gridloc(i1-1,j1  ,k1); icell3=h%cellloc(i1-1,j1  ,k1)
           if(.not.ok1 .or. .not.ok3)then
              weight=1.0
              if(ok1 .or. ok2 .or. ok3)weight=0.5
#if NDIM==3
              dflux=(h%emfz(i3,j3,k3)+h%emfz(i3,j3,k3+1))*0.25*weight
#else
              dflux=(h%emfz(i3,j3,k3))*0.5*weight
#endif
              if(.not.ok1)m%bnew(icell1,5,igrid1)=m%bnew(icell1,5,igrid1)+dflux
              if(.not.ok1)m%bnew(icell1,1,igrid1)=m%bnew(icell1,1,igrid1)+dflux
              if(.not.ok2)m%bnew(icell2,4,igrid2)=m%bnew(icell2,4,igrid2)+dflux
              if(.not.ok2)m%bnew(icell2,5,igrid2)=m%bnew(icell2,5,igrid2)-dflux
              if(.not.ok3)m%bnew(icell3,2,igrid3)=m%bnew(icell3,2,igrid3)-dflux
              if(.not.ok3)m%bnew(icell3,4,igrid3)=m%bnew(icell3,4,igrid3)-dflux
           endif

           ! Update coarse Bx and By using fine EMFz on X=0 and Y=1 grid edge
           ok1=(h%childloc(i1-1,j1  ,k1)>0); igrid1=h%gridloc(i1-1,j1  ,k1); icell1=h%cellloc(i1-1,j1  ,k1)
           ok2=(h%childloc(i1-1,j1+1,k1)>0); igrid2=h%gridloc(i1-1,j1+1,k1); icell2=h%cellloc(i1-1,j1+1,k1)
           ok3=(h%childloc(i1  ,j1+1,k1)>0); igrid3=h%gridloc(i1  ,j1+1,k1); icell3=h%cellloc(i1  ,j1+1,k1)
           if(.not.ok1 .or. .not.ok3)then
              weight=1.0
              if(ok1 .or. ok2 .or. ok3)weight=0.5
#if NDIM==3
              dflux=(h%emfz(i3,j3+2,k3)+h%emfz(i3,j3+2,k3+1))*0.25*weight
#else
              dflux=(h%emfz(i3,j3+2,k3))*0.5*weight
#endif
              if(.not.ok1)m%bnew(icell1,4,igrid1)=m%bnew(icell1,4,igrid1)+dflux
              if(.not.ok1)m%bnew(icell1,5,igrid1)=m%bnew(icell1,5,igrid1)-dflux
              if(.not.ok2)m%bnew(icell2,2,igrid2)=m%bnew(icell2,2,igrid2)-dflux
              if(.not.ok2)m%bnew(icell2,4,igrid2)=m%bnew(icell2,4,igrid2)-dflux
              if(.not.ok3)m%bnew(icell3,1,igrid3)=m%bnew(icell3,1,igrid3)-dflux
              if(.not.ok3)m%bnew(icell3,2,igrid3)=m%bnew(icell3,2,igrid3)+dflux
           endif

           ! Update coarse Bx and By using fine EMFz on X=1 and Y=1 grid edge
           ok1=(h%childloc(i1  ,j1+1,k1)>0); igrid1=h%gridloc(i1  ,j1+1,k1); icell1=h%cellloc(i1  ,j1+1,k1)
           ok2=(h%childloc(i1+1,j1+1,k1)>0); igrid2=h%gridloc(i1+1,j1+1,k1); icell2=h%cellloc(i1+1,j1+1,k1)
           ok3=(h%childloc(i1+1,j1  ,k1)>0); igrid3=h%gridloc(i1+1,j1  ,k1); icell3=h%cellloc(i1+1,j1  ,k1)
           if(.not.ok1 .or. .not.ok3)then
              weight=1.0
              if(ok1 .or. ok2 .or. ok3)weight=0.5
#if NDIM==3
              dflux=(h%emfz(i3+2,j3+2,k3)+h%emfz(i3+2,j3+2,k3+1))*0.25*weight
#else
              dflux=(h%emfz(i3+2,j3+2,k3))*0.5*weight
#endif
              if(.not.ok1)m%bnew(icell1,2,igrid1)=m%bnew(icell1,2,igrid1)-dflux
              if(.not.ok1)m%bnew(icell1,4,igrid1)=m%bnew(icell1,4,igrid1)-dflux
              if(.not.ok2)m%bnew(icell2,1,igrid2)=m%bnew(icell2,1,igrid2)-dflux
              if(.not.ok2)m%bnew(icell2,2,igrid2)=m%bnew(icell2,2,igrid2)+dflux
              if(.not.ok3)m%bnew(icell3,5,igrid3)=m%bnew(icell3,5,igrid3)+dflux
              if(.not.ok3)m%bnew(icell3,1,igrid3)=m%bnew(icell3,1,igrid3)+dflux
           endif

           ! Update coarse Bx and By using fine EMFz on X=1 and Y=0 grid edge
           ok1=(h%childloc(i1+1,j1  ,k1)>0); igrid1=h%gridloc(i1+1,j1  ,k1); icell1=h%cellloc(i1+1,j1  ,k1)
           ok2=(h%childloc(i1+1,j1-1,k1)>0); igrid2=h%gridloc(i1+1,j1-1,k1); icell2=h%cellloc(i1+1,j1-1,k1)
           ok3=(h%childloc(i1  ,j1-1,k1)>0); igrid3=h%gridloc(i1  ,j1-1,k1); icell3=h%cellloc(i1  ,j1-1,k1)
           if(.not.ok1 .or. .not.ok3)then
              weight=1.0
              if(ok1 .or. ok2 .or. ok3)weight=0.5
#if NDIM==3
              dflux=(h%emfz(i3+2,j3,k3)+h%emfz(i3+2,j3,k3+1))*0.25*weight
#else
              dflux=(h%emfz(i3+2,j3,k3))*0.5*weight
#endif
              if(.not.ok1)m%bnew(icell1,1,igrid1)=m%bnew(icell1,1,igrid1)-dflux
              if(.not.ok1)m%bnew(icell1,2,igrid1)=m%bnew(icell1,2,igrid1)+dflux
              if(.not.ok2)m%bnew(icell2,5,igrid2)=m%bnew(icell2,5,igrid2)+dflux
              if(.not.ok2)m%bnew(icell2,1,igrid2)=m%bnew(icell2,1,igrid2)+dflux
              if(.not.ok3)m%bnew(icell3,4,igrid3)=m%bnew(icell3,4,igrid3)+dflux
              if(.not.ok3)m%bnew(icell3,5,igrid3)=m%bnew(icell3,5,igrid3)-dflux
           endif
#if NDIM==3
           !--------------------------------------
           ! Deal with 4 EMFx edges
           !--------------------------------------

           ! Update coarse By and Bz using fine EMFx on Y=0 and Z=0 grid edge
           ok1=(h%childloc(i1,j1  ,k1-1)>0); igrid1=h%gridloc(i1,j1  ,k1-1); icell1=h%cellloc(i1,j1  ,k1-1)
           ok2=(h%childloc(i1,j1-1,k1-1)>0); igrid2=h%gridloc(i1,j1-1,k1-1); icell2=h%cellloc(i1,j1-1,k1-1)
           ok3=(h%childloc(i1,j1-1,k1  )>0); igrid3=h%gridloc(i1,j1-1,k1  ); icell3=h%cellloc(i1,j1-1,k1  )
           if(.not.ok1 .or. .not.ok3)then
              weight=1.0
              if(ok1 .or. ok2 .or. ok3)weight=0.5
              dflux=(h%emfx(i3,j3,k3)+h%emfx(i3+1,j3,k3))*0.25*weight
              if(.not.ok1)m%bnew(icell1,6,igrid1)=m%bnew(icell1,6,igrid1)+dflux
              if(.not.ok1)m%bnew(icell1,2,igrid1)=m%bnew(icell1,2,igrid1)+dflux
              if(.not.ok2)m%bnew(icell2,5,igrid2)=m%bnew(icell2,5,igrid2)+dflux
              if(.not.ok2)m%bnew(icell2,6,igrid2)=m%bnew(icell2,6,igrid2)-dflux
              if(.not.ok3)m%bnew(icell3,3,igrid3)=m%bnew(icell3,3,igrid3)-dflux
              if(.not.ok3)m%bnew(icell3,5,igrid3)=m%bnew(icell3,5,igrid3)-dflux
           endif

           ! Update coarse By and Bz using fine EMFx on Y=0 and Z=1 grid edge
           ok1=(h%childloc(i1,j1-1,k1  )>0); igrid1=h%gridloc(i1,j1-1,k1  ); icell1=h%cellloc(i1,j1-1,k1  )
           ok2=(h%childloc(i1,j1-1,k1+1)>0); igrid2=h%gridloc(i1,j1-1,k1+1); icell2=h%cellloc(i1,j1-1,k1+1)
           ok3=(h%childloc(i1,j1  ,k1+1)>0); igrid3=h%gridloc(i1,j1  ,k1+1); icell3=h%cellloc(i1,j1  ,k1+1)
           if(.not.ok1 .or. .not.ok3)then
              weight=1.0
              if(ok1 .or. ok2 .or. ok3)weight=0.5
              dflux=(h%emfx(i3,j3,k3+2)+h%emfx(i3+1,j3,k3+2))*0.25*weight
              if(.not.ok1)m%bnew(icell1,5,igrid1)=m%bnew(icell1,5,igrid1)+dflux
              if(.not.ok1)m%bnew(icell1,6,igrid1)=m%bnew(icell1,6,igrid1)-dflux
              if(.not.ok2)m%bnew(icell2,3,igrid2)=m%bnew(icell2,3,igrid2)-dflux
              if(.not.ok2)m%bnew(icell2,5,igrid2)=m%bnew(icell2,5,igrid2)-dflux
              if(.not.ok3)m%bnew(icell3,2,igrid3)=m%bnew(icell3,2,igrid3)-dflux
              if(.not.ok3)m%bnew(icell3,3,igrid3)=m%bnew(icell3,3,igrid3)+dflux
           endif

           ! Update coarse By and Bz using fine EMFx on Y=1 and Z=1 grid edge
           ok1=(h%childloc(i1,j1  ,k1+1)>0); igrid1=h%gridloc(i1,j1  ,k1+1); icell1=h%cellloc(i1,j1  ,k1+1)
           ok2=(h%childloc(i1,j1+1,k1+1)>0); igrid2=h%gridloc(i1,j1+1,k1+1); icell2=h%cellloc(i1,j1+1,k1+1)
           ok3=(h%childloc(i1,j1+1,k1  )>0); igrid3=h%gridloc(i1,j1+1,k1  ); icell3=h%cellloc(i1,j1+1,k1  )
           if(.not.ok1 .or. .not.ok3)then
              weight=1.0
              if(ok1 .or. ok2 .or. ok3)weight=0.5
              dflux=(h%emfx(i3,j3+2,k3+2)+h%emfx(i3+1,j3+2,k3+2))*0.25*weight
              if(.not.ok1)m%bnew(icell1,3,igrid1)=m%bnew(icell1,3,igrid1)-dflux
              if(.not.ok1)m%bnew(icell1,5,igrid1)=m%bnew(icell1,5,igrid1)-dflux
              if(.not.ok2)m%bnew(icell2,2,igrid2)=m%bnew(icell2,2,igrid2)-dflux
              if(.not.ok2)m%bnew(icell2,3,igrid2)=m%bnew(icell2,3,igrid2)+dflux
              if(.not.ok3)m%bnew(icell3,6,igrid3)=m%bnew(icell3,6,igrid3)+dflux
              if(.not.ok3)m%bnew(icell3,2,igrid3)=m%bnew(icell3,2,igrid3)+dflux
           endif

           ! Update coarse By and Bz using fine EMFx on Y=1 and Z=0 grid edge
           ok1=(h%childloc(i1,j1+1,k1  )>0); igrid1=h%gridloc(i1,j1+1,k1  ); icell1=h%cellloc(i1,j1+1,k1  )
           ok2=(h%childloc(i1,j1+1,k1-1)>0); igrid2=h%gridloc(i1,j1+1,k1-1); icell2=h%cellloc(i1,j1+1,k1-1)
           ok3=(h%childloc(i1,j1  ,k1-1)>0); igrid3=h%gridloc(i1,j1  ,k1-1); icell3=h%cellloc(i1,j1  ,k1-1)
           if(.not.ok1 .or. .not.ok3)then
              weight=1.0
              if(ok1 .or. ok2 .or. ok3)weight=0.5
              dflux=(h%emfx(i3,j3+2,k3)+h%emfx(i3+1,j3+2,k3))*0.25*weight
              if(.not.ok1)m%bnew(icell1,2,igrid1)=m%bnew(icell1,2,igrid1)-dflux
              if(.not.ok1)m%bnew(icell1,3,igrid1)=m%bnew(icell1,3,igrid1)+dflux
              if(.not.ok2)m%bnew(icell2,6,igrid2)=m%bnew(icell2,6,igrid2)+dflux
              if(.not.ok2)m%bnew(icell2,2,igrid2)=m%bnew(icell2,2,igrid2)+dflux
              if(.not.ok3)m%bnew(icell3,5,igrid3)=m%bnew(icell3,5,igrid3)+dflux
              if(.not.ok3)m%bnew(icell3,6,igrid3)=m%bnew(icell3,6,igrid3)-dflux
           endif

           !--------------------------------------
           ! Deal with 4 EMFy edges
           !--------------------------------------

           ! Update coarse Bx and Bz using fine EMFy on X=0 and Z=0 grid edge
           ok1=(h%childloc(i1  ,j1,k1-1)>0); igrid1=h%gridloc(i1  ,j1,k1-1); icell1=h%cellloc(i1  ,j1,k1-1)
           ok2=(h%childloc(i1-1,j1,k1-1)>0); igrid2=h%gridloc(i1-1,j1,k1-1); icell2=h%cellloc(i1-1,j1,k1-1)
           ok3=(h%childloc(i1-1,j1,k1  )>0); igrid3=h%gridloc(i1-1,j1,k1  ); icell3=h%cellloc(i1-1,j1,k1  )

           if(.not.ok1 .or. .not.ok3)then
              weight=1.0
              if(ok1 .or. ok2 .or. ok3)weight=0.5
              dflux=(h%emfy(i3,j3,k3)+h%emfy(i3,j3+1,k3))*0.25*weight
              if(.not.ok1)m%bnew(icell1,6,igrid1)=m%bnew(icell1,6,igrid1)-dflux
              if(.not.ok1)m%bnew(icell1,1,igrid1)=m%bnew(icell1,1,igrid1)-dflux
              if(.not.ok2)m%bnew(icell2,4,igrid2)=m%bnew(icell2,4,igrid2)-dflux
              if(.not.ok2)m%bnew(icell2,6,igrid2)=m%bnew(icell2,6,igrid2)+dflux
              if(.not.ok3)m%bnew(icell3,3,igrid3)=m%bnew(icell3,3,igrid3)+dflux
              if(.not.ok3)m%bnew(icell3,4,igrid3)=m%bnew(icell3,4,igrid3)+dflux
           endif

           ! Update coarse Bx and Bz using fine EMFy on X=0 and Z=1 grid edge
           ok1=(h%childloc(i1-1,j1,k1  )>0); igrid1=h%gridloc(i1-1,j1,k1  ); icell1=h%cellloc(i1-1,j1,k1  )
           ok2=(h%childloc(i1-1,j1,k1+1)>0); igrid2=h%gridloc(i1-1,j1,k1+1); icell2=h%cellloc(i1-1,j1,k1+1)
           ok3=(h%childloc(i1  ,j1,k1+1)>0); igrid3=h%gridloc(i1  ,j1,k1+1); icell3=h%cellloc(i1  ,j1,k1+1)
           if(.not.ok1 .or. .not.ok3)then
              weight=1.0
              if(ok1 .or. ok2 .or. ok3)weight=0.5
              dflux=(h%emfy(i3,j3,k3+2)+h%emfy(i3,j3+1,k3+2))*0.25*weight
              if(.not.ok1)m%bnew(icell1,4,igrid1)=m%bnew(icell1,4,igrid1)-dflux
              if(.not.ok1)m%bnew(icell1,6,igrid1)=m%bnew(icell1,6,igrid1)+dflux
              if(.not.ok2)m%bnew(icell2,3,igrid2)=m%bnew(icell2,3,igrid2)+dflux
              if(.not.ok2)m%bnew(icell2,4,igrid2)=m%bnew(icell2,4,igrid2)+dflux
              if(.not.ok3)m%bnew(icell3,1,igrid3)=m%bnew(icell3,1,igrid3)+dflux
              if(.not.ok3)m%bnew(icell3,3,igrid3)=m%bnew(icell3,3,igrid3)-dflux
           endif

           ! Update coarse Bx and Bz using fine EMFy on X=1 and Z=1 grid edge
           ok1=(h%childloc(i1  ,j1,k1+1)>0); igrid1=h%gridloc(i1  ,j1,k1+1); icell1=h%cellloc(i1  ,j1,k1+1)
           ok2=(h%childloc(i1+1,j1,k1+1)>0); igrid2=h%gridloc(i1+1,j1,k1+1); icell2=h%cellloc(i1+1,j1,k1+1)
           ok3=(h%childloc(i1+1,j1,k1  )>0); igrid3=h%gridloc(i1+1,j1,k1  ); icell3=h%cellloc(i1+1,j1,k1  )
           if(.not.ok1 .or. .not.ok3)then
              weight=1.0
              if(ok1 .or. ok2 .or. ok3)weight=0.5
              dflux=(h%emfy(i3+2,j3,k3+2)+h%emfy(i3+2,j3+1,k3+2))*0.25*weight
              if(.not.ok1)m%bnew(icell1,3,igrid1)=m%bnew(icell1,3,igrid1)+dflux
              if(.not.ok1)m%bnew(icell1,4,igrid1)=m%bnew(icell1,4,igrid1)+dflux
              if(.not.ok2)m%bnew(icell2,1,igrid2)=m%bnew(icell2,1,igrid2)+dflux
              if(.not.ok2)m%bnew(icell2,3,igrid2)=m%bnew(icell2,3,igrid2)-dflux
              if(.not.ok3)m%bnew(icell3,6,igrid3)=m%bnew(icell3,6,igrid3)-dflux
              if(.not.ok3)m%bnew(icell3,1,igrid3)=m%bnew(icell3,1,igrid3)-dflux
           endif

           ! Update coarse Bx and Bz using fine EMFy on X=1 and Z=0 grid edge
           ok1=(h%childloc(i1+1,j1,k1  )>0); igrid1=h%gridloc(i1+1,j1,k1  ); icell1=h%cellloc(i1+1,j1,k1  )
           ok2=(h%childloc(i1+1,j1,k1-1)>0); igrid2=h%gridloc(i1+1,j1,k1-1); icell2=h%cellloc(i1+1,j1,k1-1)
           ok3=(h%childloc(i1  ,j1,k1-1)>0); igrid3=h%gridloc(i1  ,j1,k1-1); icell3=h%cellloc(i1  ,j1,k1-1)
           if(.not.ok1 .or. .not.ok3)then
              weight=1.0
              if(ok1 .or. ok2 .or. ok3)weight=0.5
              dflux=(h%emfy(i3+2,j3,k3)+h%emfy(i3+2,j3+1,k3))*0.25*weight
              if(.not.ok1)m%bnew(icell1,1,igrid1)=m%bnew(icell1,1,igrid1)+dflux
              if(.not.ok1)m%bnew(icell1,3,igrid1)=m%bnew(icell1,3,igrid1)-dflux
              if(.not.ok2)m%bnew(icell2,6,igrid2)=m%bnew(icell2,6,igrid2)-dflux
              if(.not.ok2)m%bnew(icell2,1,igrid2)=m%bnew(icell2,1,igrid2)-dflux
              if(.not.ok3)m%bnew(icell3,4,igrid3)=m%bnew(icell3,4,igrid3)-dflux
              if(.not.ok3)m%bnew(icell3,6,igrid3)=m%bnew(icell3,6,igrid3)+dflux
           endif
#endif
        end do
     end do
  end do
#endif
#endif

  endif

  !----------------
  ! Unlock all octs
  !----------------
  do k1=k1min,k1max
     do j1=j1min,j1max
        do i1=i1min,i1max     
           ! Get oct index
           ichild=h%childloc(i1,j1,k1)
           ! Check that parent cell is not refined
           if(ichild>0)then
              call unlock_cache(m,ichild)
           else
              ! Get parent cell index
              igrid=h%gridloc(i1,j1,k1)
              call unlock_cache(m,igrid)
              ! Get neighbouring parent oct index
              do inbor=1,twondim
                 igrid=h%nborloc(i1,j1,k1,inbor)
                 call unlock_cache(m,igrid)
#ifdef MHD
                 igrid=h%nborsonloc(i1,j1,k1,inbor)
                 call unlock_cache(m,igrid)
#endif
              end do
           endif
        end do
     end do
  end do

  end associate

#endif

end subroutine godfine1
!###########################################################
!###########################################################
!###########################################################
!###########################################################
subroutine init_flush_godunov(mesh,igrid,hash_key)
  use amr_parameters, only: ndim, twotondim
  use hydro_parameters, only: nvar
  use amr_commons, only: mesh_t
  type(mesh_t)::mesh
  integer::igrid
  integer(kind=8),dimension(0:ndim)::hash_key

  integer::ind,ivar

  mesh%grid(igrid)%lev=int(hash_key(0),kind=4)
  mesh%grid(igrid)%ckey(1:ndim)=int(hash_key(1:ndim),kind=4)

#ifdef HYDRO
  do ivar=1,nvar
     do ind=1,twotondim
        mesh%unew(ind,ivar,igrid)=0.0d0
     enddo
  enddo
#ifdef TRCFLX
  mesh%mflux(:,:,igrid)=0.0d0
#endif
#endif

#ifdef MHD
  do ivar=1,6
     do ind=1,twotondim
        mesh%bnew(ind,ivar,igrid)=0.0d0
     end do
  end do
#endif

end subroutine init_flush_godunov
!###########################################################
!###########################################################
!###########################################################
!###########################################################
subroutine pack_flush_godunov(mesh,igrid,msg_size,msg_array)
  use amr_parameters, only: twotondim
  use hydro_parameters, only: nvar
  use amr_commons, only: mesh_t
  use cache_commons, only: msg_large_realdp
  type(mesh_t)::mesh
  integer::igrid
  integer::msg_size
  integer,dimension(1:msg_size),optional::msg_array

  integer::ind,ivar
  type(msg_large_realdp)::msg

#ifdef HYDRO
  do ivar=1,nvar
     do ind=1,twotondim
        msg%realdp_hydro(ind,ivar)=mesh%unew(ind,ivar,igrid)
     end do
  end do
#ifdef TRCFLX
  msg%realdp_mflux=mesh%mflux(:,:,igrid)
#endif
#endif
#ifdef MHD
  do ivar=1,6
     do ind=1,twotondim
        msg%realdp_mhd(ind,ivar)=mesh%bnew(ind,ivar,igrid)
     end do
  end do
#endif

  msg_array=transfer(msg,msg_array)

end subroutine pack_flush_godunov
!###########################################################
!###########################################################
!###########################################################
!###########################################################
subroutine unpack_flush_godunov(mesh,igrid,msg_size,msg_array,hash_key)
  use amr_parameters, only: ndim, twotondim
  use hydro_parameters, only: nvar
  use amr_commons, only: mesh_t
  use cache_commons, only: msg_large_realdp
  type(mesh_t)::mesh
  integer::igrid
  integer::msg_size
  integer,dimension(1:msg_size),optional::msg_array
  integer(kind=8),dimension(0:ndim)::hash_key

  integer::ind,ivar
  type(msg_large_realdp)::msg

  mesh%grid(igrid)%lev=int(hash_key(0),kind=4)
  mesh%grid(igrid)%ckey(1:ndim)=int(hash_key(1:ndim),kind=4)
  msg=transfer(msg_array,msg)

#ifdef HYDRO
  do ivar=1,nvar
     do ind=1,twotondim
        mesh%unew(ind,ivar,igrid)=mesh%unew(ind,ivar,igrid)+msg%realdp_hydro(ind,ivar)
     end do
  end do
#ifdef TRCFLX
  mesh%mflux(:,:,igrid)=mesh%mflux(:,:,igrid)+msg%realdp_mflux
#endif
#endif

#ifdef MHD
  do ivar=1,6
     do ind=1,twotondim
        mesh%bnew(ind,ivar,igrid)=mesh%bnew(ind,ivar,igrid)+msg%realdp_mhd(ind,ivar)
     end do
  end do
#endif

end subroutine unpack_flush_godunov
!###########################################################
!###########################################################
!###########################################################
!###########################################################
end module godunov_fine_module
